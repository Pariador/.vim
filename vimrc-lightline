"------COMPONENTS------
let s:component_min_widths = {
    \ 'full_mode': 68,
    \ 'file_relativepath': 107,
    \ 'file_name': 30,
    \ 'flags': 40,
    \ 'filetype': 120,
    \ 'filedata': 140,
    \ 'percent': 63,
    \
    \ 'lineinfo_glyph': 56,
    \ 'lineinfo_max_lines': 56,
    \ 'lineinfo': 51,
    \
    \ 'noscrollbar': 93,
    \ 'nerd_tree_dir': 56,
    \ 'buffergator': 25,
    \
    \ 'readonly': 65,
    \ 'modified': 65,
\ }

function! s:StatusGetSpecial()
    if exists('b:NERDTree')
        return 'NERDTree'
    elseif exists('b:buffergator_catalog_viewer')
        return 'Buffergator'
    elseif exists('b:plug_preview')
        return 'Plug'
    endif

    return ''
endfunction

" Mode
let s:mode_map = {
    \ 'n': 'NORMAL',
    \
    \ 'i': 'INSERT',
    \
    \ 'R': 'REPLACE',
    \ 'Rv': 'R-VIRTUAL',
    \
    \ 'v': 'VISUAL',
    \ 'V': 'V-LINE',
    \ '': 'V-BLOCK',
    \
    \ 's': 'SELECT',
    \ 'S': 'S-LINE',
    \ '': 'S-BLOCK',
    \
    \ 'c': 'COMMAND',
    \ 't': 'TERMINAL',
    \
    \ 'NERDTree': 'NERD',
    \ 'Buffergator': 'BUFFERS',
    \ 'Plug': 'Plug',
\ }

let s:brief_mode_map = {
    \ 'n': 'N',
    \
    \ 'i': 'I',
    \
    \ 'R': 'R',
    \ 'Rv': 'RV',
    \
    \ 'v': 'V',
    \ 'V': 'VL',
    \ '': 'VB',
    \
    \ 's': 'S',
    \ 'S': 'SL',
    \ '': 'SB',
    \
    \ 'c': 'C',
    \ 't': 'T',
    \
    \ 'NERDTree': 'NR',
    \ 'Buffergator': 'BF',
    \ 'Plug': 'Plug',
\ }

function! s:StatusMode()
    let l:default = 'n'

    let l:map = s:mode_map
    if winwidth(0) < s:component_min_widths.full_mode
        let l:map = s:brief_mode_map
    endif

    let l:mode = s:StatusGetSpecial()

    let l:mode = l:mode == '' ? mode(1) : l:mode

    if has_key(l:map, l:mode)
        return l:map[l:mode]
    endif

    return l:map[l:default]
endfunction

" File
function! s:StatusToDisplayFile()
    return s:StatusGetSpecial() == ''
endfunction

function! s:StatusFile()
    if !s:StatusToDisplayFile() || winwidth(0) < s:component_min_widths.file_name
        return ''
    elseif winwidth(0) >= s:component_min_widths.file_relativepath
        return expand('%')
    else
        return expand('%:t')
    endif
endfunction

" Flags
function! s:StatusGetFlags()
    let l:flags = [
        \ &previewwindow ? 'PRV' : '',
        \ &buftype == 'help' ? 'HELP' : '',
        \ &readonly ? 'RO' : '',
        \ !&modifiable ? '-' : '',
        \ &modified ? '+' : '',
    \]

    return filter(l:flags, {i, value -> value != ''})
endfunction

function! s:StatusToDisplayFlags(...)
    if s:StatusGetSpecial() != ''
        return 0
    endif

    if a:0
        let l:flags = a:1
    else
        let l:flags = s:StatusGetFlags()
    endif

    return !empty(l:flags)

endfunction

function! s:StatusFlags()
    let l:flags = s:StatusGetFlags()

    if !s:StatusToDisplayFlags(l:flags) ||
        \ winwidth(0) < s:component_min_widths.flags
        return ''
    endif

    let l:flags = map(l:flags, {i, value ->
        \ printf(g:fat_bracket_format, value)
    \ })

    return join(l:flags, ' ')
endfunction

" FileType
function! s:StatusToDisplayFileType()
    return s:StatusGetSpecial() == '' && &filetype != ''
endfunction

function! s:StatusFileType()
    if !s:StatusToDisplayFileType() ||
        \ winwidth(0) < s:component_min_widths.filetype
        return ''
    endif

    return &filetype
endfunction

" FileData
function! s:StatusToDisplayFileData()
    if s:StatusGetSpecial() != '' ||
        \ (&fileencoding == '' && &fileformat == '')
        return 0
    endif

    return 1
endfunction

function! s:StatusFileData()
    if !s:StatusToDisplayFileData() ||
        \ winwidth(0) < s:component_min_widths.filedata
        return ''
    endif

    let l:data = &fileencoding

    let l:data .= &fileformat == '' ? '' : '[' . &fileformat . ']'

    return l:data
endfunction

" Percent
function! s:StatusToDisplayPercent()
    return s:StatusGetSpecial() == ''
endfunction

function! s:StatusPercent()
    if !s:StatusToDisplayPercent() || winwidth(0) < s:component_min_widths.percent
        return ''
    endif

    let l:percent = ((line('.') * 1.0) / line('$')) * 100

    return printf('%3d%%', float2nr(l:percent))
endfunction

" LineInfo
function! s:StatusToDisplayLineInfo()
    let l:special = s:StatusGetSpecial()
    return l:special == '' || l:special == 'Plug'
endfunction

function! s:StatusLineInfo()
    let l:winwidth = winwidth(0)

    if !s:StatusToDisplayLineInfo() ||
        \ l:winwidth < s:component_min_widths.lineinfo
        return ''
    endif

    let l:line = printf('%02d', line('.'))
    let l:col = printf('%02d', col('.'))
    let l:glyph = ''
    let l:max_lines = ''

    if l:winwidth >= s:component_min_widths.lineinfo_glyph
        let l:glyph = '☰ '
    endif

    if l:winwidth >= s:component_min_widths.lineinfo_max_lines
        let l:max_lines = printf('/%d', line('$'))
    endif

    return printf('%s[%s%s : %s]', l:glyph, l:line, l:max_lines, l:col)
endfunction

" NoScrollbar
function! s:StatusToDisplayNoscrollbar()
    return s:StatusGetSpecial() == ''
endfunction

function! s:StatusNoscrollbar()
    if !s:StatusToDisplayNoscrollbar() ||
        \ winwidth(0) < s:component_min_widths.noscrollbar
        return ''
    endif

    return noscrollbar#statusline(20,'■','◫',['◧'],['◨'])
endfunction

" NERDTree
function! s:StatusNERDTreeDir()
    if s:StatusGetSpecial() != 'NERDTree' ||
        \ winwidth(0) < s:component_min_widths.nerd_tree_dir
        return ''
    endif

    return b:NERDTree.root.path.str()
endfunction

function! s:StatusToDisplayNERDTree()
    return s:StatusGetSpecial() == 'NERDTree'
endfunction

" Buffergator
function! s:StatusBuffergator()
    let l:line = line('.')

    if s:StatusGetSpecial() != 'Buffergator' ||
        \ winwidth(0) < s:component_min_widths.buffergator ||
        \ !has_key(b:buffergator_catalog_viewer.jump_map, l:line)
        return ''
    endif

    let l:buffers_count = len(b:buffergator_catalog_viewer.buffers_catalog)

    return printf('Buffer %d of %d', l:line, l:buffers_count)
endfunction

function! s:StatusToDisplayBuffergator()
    return s:StatusGetSpecial() == 'Buffergator'
endfunction

" Tabs
function! s:TabsTabNum(tab)
    return printf(g:fat_bracket_format, a:tab)
endfunction

function! s:TabsWinCount(tab)
    let l:win_count = len(tabpagebuflist(a:tab))

    if l:win_count < 2
        return ''
    endif

    return printf('|%d|', l:win_count)
endfunction

"------SETTINGS------
let g:lightline = {}

let g:lightline.colorscheme = 'molokai_mod'

let g:lightline.separator = { 'left': '', 'right': '' }

let g:lightline.subseparator = { 'left': '', 'right': '' }

let g:lightline.component = {}
"\ }

let g:lightline.component_visible_condition = {
\ }

" Funcitons that pull and format the status line info.
let g:lightline.component_function = {
    \ 'mode': get(funcref('s:StatusMode'), 'name'),
    \ 'file': get(funcref('s:StatusFile'), 'name'),
    \ 'flags': get(funcref('s:StatusFlags'), 'name'),
    \ 'filetype': get(funcref('s:StatusFileType'), 'name'),
    \ 'filedata': get(funcref('s:StatusFileData'), 'name'),
    \ 'percent': get(funcref('s:StatusPercent'), 'name'),
    \ 'lineinfo': get(funcref('s:StatusLineInfo'), 'name'),
    \
    \ 'noscrollbar': get(funcref('s:StatusNoscrollbar'), 'name'),
    \ 'nerd_tree_dir': get(funcref('s:StatusNERDTreeDir'), 'name'),
    \ 'buffergator': get(funcref('s:StatusBuffergator'), 'name'),
\ }

" Functions that check whether to display status line info.
let g:lightline.component_function_visible_condition = {
    \ 'file': get(funcref('s:StatusToDisplayFile'), 'name') . '()',
    \ 'flags': get(funcref('s:StatusToDisplayFlags'), 'name') . '()',
    \ 'filetype': get(funcref('s:StatusToDisplayFileType'), 'name') . '()',
    \ 'filedata': get(funcref('s:StatusToDisplayFileData'), 'name') . '()',
    \ 'percent': get(funcref('s:StatusToDisplayPercent'), 'name') . '()',
    \ 'lineinfo': get(funcref('s:StatusToDisplayLineInfo'), 'name') . '()',
    \
    \ 'noscrollbar': get(funcref('s:StatusToDisplayNoscrollbar'), 'name') . '()',
    \ 'nerd_tree_dir': get(funcref('s:StatusToDisplayNERDTree'), 'name') . '()',
    \ 'buffergator': get(funcref('s:StatusToDisplayBuffergator'), 'name') . '()',
\ }

let lightline.tab_component_function = {
    \ 'tabnum': get(funcref('s:TabsTabNum'), 'name'),
    \ 'wincount': get(funcref('s:TabsWinCount'), 'name'),
\ }

let g:lightline.mode_map = {
\ }

" For active windows.
let g:lightline.active = {
    \ 'left': [
        \ ['mode', 'paste'],
        \ ['buffergator', 'nerd_tree_dir', 'file', 'flags'],
        \ ['filetype', 'filedata']
    \ ],
    \ 'right': [['percent', 'lineinfo', 'linetest'], ['noscrollbar']]
\ }

" For inactive windows.
let g:lightline.inactive = {
    \ 'left': [['nerd_tree_dir', 'file', 'flags']],
\ }

let g:lightline.tabline = { 'right': [] }

let g:lightline.tab = {
    \ 'active': [ 'tabnum', 'filename', 'modified', 'wincount'],
    \ 'inactive': [ 'tabnum', 'filename', 'modified', 'wincount']
\ }
